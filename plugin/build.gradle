plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "1.0.0"
}

group = "edu.illinois"
version = "1.1.3-SNAPSHOT"

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    implementation 'edu.illinois:nondex-common:' + version
    implementation 'edu.illinois:nondex-instrumentation:' + version
}

gradlePlugin {
    plugins {
        nondexPlugin {
            id = 'edu.illinois.nondex'
            implementationClass = 'edu.illinois.nondex.gradle.plugin.NondexGradlePlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
    // Compile java with groovy to allow Java class to access Groovy class
    main {
        java {
            srcDirs = []
        }
        groovy {
            srcDirs += ['src/main/java']
        }
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('test') {
    useJUnitPlatform()
}
